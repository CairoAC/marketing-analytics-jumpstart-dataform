/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {  
  type: "view",
  schema: functions.productSchema("ga4"),
  description: "Session count by date, web browser & device category.",
  tags: ['ga4','dashboard']
}
js {
  //builds case statement to group web browsers in dashboard
  const webBrowsers = sql.dashboardWebBrowsers;
  const caseStatement = sql.webBrowserCaseStatement(webBrowsers,"device.device_web_browser");
}
WITH
  event_devices AS (
  SELECT
    DISTINCT event.event_date,
    event.ga_session_id,
    event.user_pseudo_id,
    SUM(COALESCE(event.ecommerce.purchase_revenue_in_usd,0)) as total_purchase_revenue,
    ${caseStatement} AS device_web_browser,
    INITCAP(device.device_category) AS device_category
  FROM
    ${ref({schema:functions.productSchema("ga4"),name: "event"})} event
  LEFT JOIN
    ${ref({schema:functions.productSchema("ga4"),name: "device"})} device
  ON
    event.device_type_id = device.device_type_id
  WHERE
    event.ga_session_id IS NOT NULL --only want events with a ga_session_id
  GROUP BY
    event_date,
    ga_session_id,
    user_pseudo_id,
    device_web_browser,
    device_category
)
SELECT
  COUNT(*) AS session_count,
  event_date,
  SUM(total_purchase_revenue) as total_purchase_revenue,
  device_web_browser,
  device_category
FROM
  event_devices
GROUP BY
  event_date,
  device_web_browser,
  device_category