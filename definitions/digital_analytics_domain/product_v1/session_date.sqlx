/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {  
  type: "view",
  schema: functions.productSchema("ga4"),
  description: "View that associates each session with a date, or multiple dates if the session spans days.",
  tags: ['ga4','dashboard']
}


WITH
  Sessions AS (
  SELECT
    ga_session_id, --ga_session_id and user_pseudo_id uniquely identify a session
    user_pseudo_id,
    platform,
    total_engagement_time_secs,
    EXTRACT(date
    FROM
      session_start) AS session_start_date,
    EXTRACT(date
    FROM
      session_end) AS session_end_date
  FROM
    ${ref({schema:functions.productSchema("ga4"),name: "session"})}
  )
  --unpivots to create one row per session & date
SELECT
  DISTINCT ga_session_id,
  user_pseudo_id,
  platform,
  total_engagement_time_secs as engagement_time_secs,
  session_date
FROM
  sessions UNPIVOT
  (
    session_date FOR session_date_name IN
    (session_start_date,
    session_end_date)
  )